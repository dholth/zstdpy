import sys
import enscons, enscons.cpyext
import pytoml as toml

metadata = dict(toml.load(open("pyproject.toml")))["tool"]["enscons"]

full_tag = enscons.get_binary_tag()

env = Environment(
    tools=["default", "packaging", enscons.generate, enscons.cpyext.generate],
    PACKAGE_METADATA=metadata,
    WHEEL_TAG=full_tag,
)

py_source = ["dezstd.py"]

env.Command("_dezstd.c", ["cdefs.py", "zstddeclib.c"], sys.executable + "  cdefs.py")

# get filename
from distutils import dist
from distutils.command.build_ext import build_ext

ext = build_ext(dist.Distribution(dict(name="_dezstd")))
module = env.LoadableModule(
    ext.get_ext_filename("_dezstd"), ["_dezstd.c"], LIBPREFIX="", FRAMEWORKS=[], LIBS=[]
)

platlib = env.Whl("platlib", py_source + [module], root="")
whl = env.WhlFile(platlib)

# Add automatic source files, plus any other needed files.
sdist_source = FindSourceFiles() + ["PKG-INFO", "LICENSE", "LICENSE.zstd"]

sdist = env.SDist(source=sdist_source)

env.NoClean(sdist)
env.Alias("sdist", sdist)

develop = env.Command("#DEVELOP", enscons.egg_info_targets(env), enscons.develop)
env.Alias("develop", develop)

# needed for pep517 / enscons.api to work
env.Default(whl, sdist)
