import sys
import enscons, enscons.cpyext
import pytoml as toml

metadata = dict(toml.load(open("pyproject.toml")))["tool"]["enscons"]

full_tag = enscons.get_binary_tag()

env = Environment(
    tools=["default", "packaging", enscons.generate, enscons.cpyext.generate],
    PACKAGE_METADATA=metadata,
    WHEEL_TAG=full_tag,
)

if sys.platform == "darwin":
    # OSX Sierra Framework builds
    env.Append(CCFLAGS=["-F/Library/Frameworks"])
    env.Append(LINKFLAGS=["-F/Library/Frameworks"])

py_source = ["dezstd.py"]

env.Command("_dezstd.c", ["cdefs.py", "zstddeclib.c"], sys.executable + "  cdefs.py")

# get filename
from enscons.cpyext import extension_filename

ext_filename = extension_filename("_dezstd")

module = env.SharedLibrary(ext_filename, ["_dezstd.c"], LIBPREFIX="")

platlib = env.Whl("platlib", py_source + [module], root="")

# include licenses in wheel
lic = [
    env.Command(
        env["DIST_INFO_PATH"].File(license), license, [Copy("$TARGET", "$SOURCE")]
    )
    for license in ("LICENSE", "LICENSE.zstd")
]

whl = env.WhlFile([platlib] + lic)

# Add automatic source files, plus any other needed files.
sdist_source = FindSourceFiles() + ["PKG-INFO"]

sdist = env.SDist(source=sdist_source)

env.NoClean(sdist)
env.Alias("sdist", sdist)

develop = env.Command("#DEVELOP", enscons.egg_info_targets(env), enscons.develop)
env.Alias("develop", develop)

# needed for pep517 / enscons.api to work
env.Default(whl, sdist)
